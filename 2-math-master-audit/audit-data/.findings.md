## High

### [H-1] `MathMasters::mulWadUp` function adds 1 to `x` in some situations, returning incorrect output

**Description** The `MathMasters::mulWadUp` function contains a line that, if certain conditions are met, adds 1 to `x`.

**Impact** This will return an incorrect result.

**Proof of Concept**

<details>
<summary>Code</summary>

Place the following code in the `MathMasters.t.sol` test file.

```javascript
    function testMulWadUpUnit() public {
        uint256 x = 53438770891273403451;
        uint256 y = 53438770891273403445;
        uint256 result = MathMasters.mulWadUp(x, y);
        uint256 expected = x * y == 0 ? 0 : (x * y - 1) / 1e18 + 1;
        uint256 resultDown = MathMasters.mulWad(x, y);
        console2.log("result:", result); // 2855702234370009622372
        console2.log("expected:", expected); // 2855702234370009622319
        console2.log("resultDown:", resultDown); // 2855702234370009622318
        assert(result != expected);
        assertEq(resultDown, expected - 1);
    }

```

</details>

**Recommended Mitigation** Remove this unnecessary line:

```diff
   function mulWadUp(uint256 x, uint256 y) internal pure returns (uint256 z) {
        /// @solidity memory-safe-assembly
        assembly {
            // Equivalent to `require(y == 0 || x <= type(uint256).max / y)`.
            if mul(
                y,
                gt(
                    x,
                    div(not(0), y)
                )
            ) {
                mstore(0x40, 0xbac65e5b)
                revert(0x1c, 0x04)
            }
-           if iszero(
-               sub(
-                   div(add(z, x), y), // is this zero? ((0 + x / y) - 1)
-                   1
-               )
-           ) { x := add(x, 1) }
            z :=
                add(
                    iszero(
                        iszero(
                            mod(mul(x, y), WAD)
                        )
                    ),
                    div(mul(x, y), WAD)
                )
        }
    }
```

### [H-2] `MathMasters::sqrt` contains incorrect value when comparing to `x` to shift bits, returning incorrect output

**Description** In the `MathMasters::sqrt` function, assembly is used to find the square root `r` by shifting the bits left and right, comparing the outputs each time. However there is an incorrect value being used in a line of comparison.
Each value used is a fullwidth hexadecimal (ie they consist soley of repeating `f`s), except for `16777002` - the hexadecimal value of which is `0xffff2a`.

```javascript
            // 87112285931760246646623899502532662132735 == 0xffffffffffffffffffffffffffffffffff
            let r := shl(7, lt(87112285931760246646623899502532662132735, x))
            // 4722366482869645213695 == 0xffffffffffffffffff
            r := or(r, shl(6, lt(4722366482869645213695, shr(r, x))))
            // 1099511627775 == 0xffffffffff
            r := or(r, shl(5, lt(1099511627775, shr(r, x))))
            // 16777002 == 0xffff2a
            // 16777215 == 0xffffff
            r := or(r, shl(4, lt(16777002, shr(r, x))))
```

**Impact** This will return the incorrect output.

**Proof of Concept** The `MathMasters::sqrt` function shares identical lines at the end of its implementation as its [Solmate Equivalent](https://github.com/transmissions11/solmate/blob/97bdb2003b70382996a79a406813f76417b1cf90/src/utils/FixedPointMathLib.sol#L212-L225). We can essentially remove these identical lines by creating new functions in our `Harness` contract with the differential sections of each `sqrt` function.

<details>
<summary>Code</summary>

Place the following code in the `./certora/harness/Harness.sol` file.

```javascript
function solmateTopHalf(uint256 x) external pure returns (uint256 z) {
        assembly {
            let y := x

            z := 181
            if iszero(lt(y, 0x10000000000000000000000000000000000)) {
                y := shr(128, y)
                z := shl(64, z)
            }
            if iszero(lt(y, 0x1000000000000000000)) {
                y := shr(64, y)
                z := shl(32, z)
            }
            if iszero(lt(y, 0x10000000000)) {
                y := shr(32, y)
                z := shl(16, z)
            }
            if iszero(lt(y, 0x1000000)) {
                y := shr(16, y)
                z := shl(8, z)
            }
            z := shr(18, mul(z, add(y, 65536)))
        }
    }

    function mathMastersTopHalf(uint256 x) external pure returns (uint256 z) {
        assembly {
            z := 181

            let r := shl(7, lt(87112285931760246646623899502532662132735, x))
            r := or(r, shl(6, lt(4722366482869645213695, shr(r, x))))
            r := or(r, shl(5, lt(1099511627775, shr(r, x))))
            r := or(r, shl(4, lt(16777002, shr(r, x))))
            z := shl(shr(1, r), z)
            z := shr(18, mul(z, add(shr(r, x), 65536)))
        }
    }
```

</details>

Next we can use Halmos to compare these pieces of code.

<details>
<summary>Code</summary>

Place the following code in the `MathMasters.t.sol` test file.

```javascript
function check_testHarnessWithCertoraEdgeCase(uint256 x) public {
        Harness harness = new Harness();
        assert(harness.mathMastersTopHalf(x) == harness.solmateTopHalf(x));
    }
```

</details>

This gives us multiple edge cases which we can demonstrate in a unit test.

<details>
<summary>Code</summary>

Place the following code in the `MathMasters.t.sol` test file.

```javascript
function test_sqrt_breaks() public {
        // 24519788349697037967053404880661006174729938985413509120
        // 309481597173691432457732096
        // 72057151656296448
        // 105311287332279524828995813957362719092269727931050867564254068736
        // 5708982944329400342648484220182009614862188543
        // 1329211357870787877392912416050774016
        // 452312201544546376757969088181825051580699710308692317009311379840200343552
        uint256 x = 105311293498665291426722909308999732236070323463302251608708546560;

        Harness harness = new Harness();
        uint256 mathMastersOutput = harness.mathMastersTopHalf(x);
        uint256 solmateOutput = harness.solmateTopHalf(x);
        console2.log("mathMastersOutput:", mathMastersOutput);
        console2.log("solmateOutput    :", solmateOutput);

        assert(mathMastersOutput != solmateOutput);
    }
```

</details>

**Recommended Mitigation** Replace the incorrect value with the correct, fullwidth hexadecimal.

```diff
            let r := shl(7, lt(87112285931760246646623899502532662132735, x))
            r := or(r, shl(6, lt(4722366482869645213695, shr(r, x))))
            r := or(r, shl(5, lt(1099511627775, shr(r, x))))
-           r := or(r, shl(4, lt(16777002, shr(r, x))))
+           r := or(r, shl(4, lt(16777215, shr(r, x))))
```

## Low

### [L-1] Custom errors don't work in solc 0.8.3

### [L-2] Incorrect function selector for `MathMasters::MathMasters__FullMulDivFailed()` custom error used in `MathMasters::mulWad` and `MathMasters::mulWadUp` functions

```diff
-   mstore(0x40, 0xbac65e5b)
+   mstore(0x40, 0xa56044f7)
```

## Informational

### [I-1] Free memory pointer is being overwritten
